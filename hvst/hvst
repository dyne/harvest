#!/usr/bin/env perl
use strict;
use warnings;
use feature 'say';
use Getopt::Long;

# run harvest and parse its output
sub harvest {
  my @lines = `harvest --output=csv --file 2> /dev/null | sed 1d`;
  my @files;
  my $i = 1;
  for (@lines) {
    /^(\w+),(\w+),(\d+),(\d+),(.*)$/;
    my @lt = localtime($3);
    my $file = {
      i  => $i,
      nt => $1,
      t  => $2,
      mt => $3,
      s  => $4,
      n  => $5
    };
    $file->{n} =~ s/\s*$//;
    $file->{year} = $lt[5]+1900;
    $file->{month} = sprintf('%02d', $lt[4]+1);
    $file->{day} = sprintf('%02d', $lt[3]);
    $file->{d} = "$file->{year}-$file->{month}-$file->{day}";
    push @files, $file;
    $i++;
  }
  return @files;
}

# evaluate a destination expression
sub expr {
  my $file = shift;
  my $expr = shift;
  if ($expr =~ /^@/) {
    # leading '@' means eval as code
    $expr =~ s/^@//;
    $_ = $file;
    # add variables for ease of use in expression
    my $i = $_->{i};
    my $nt = $_->{nt};
    my $t = $_->{t};
    my $d = $_->{d};
    my $mt = $_->{mt};
    my $s = $_->{s};
    my $n = $_->{n};
    my $year = $_->{year};
    my $month = $_->{month};
    my $day = $_->{day};
    my $dest = eval($expr);
    $dest =~ s/^~/\$HOME/; # do tilde expansion manually
    return $dest;
  } else {
    # just a string
    return $expr
  }
}

# list files by type
sub ls {
  my $type = shift;
  return grep { $_->{t} eq $type } harvest();
}

# list types of files and their count
sub types {
  my %count;
  for (harvest()) {
    if (exists $count{$_->{t}}) {
      $count{$_->{t}}++;
    } else {
      $count{$_->{t}} = 1;
    }
  }
  my @pairs;
  while (my @p = each %count) {
    push @pairs, \@p;
  }
  return \@pairs;
}

# A function to generate functions for filesystem operations
sub filesystem_fn {
  my $op = shift;
  return sub {
    my $type = shift;
    my $dest_expr = shift;
    my $verbose = shift;
    my $recon = shift;
    my @files = ls $type;
    for (@files) {
      my $dest = expr($_, $dest_expr);
      my $mkdir = qq[mkdir -p "$dest"];
      say $mkdir if ($verbose || $recon);
      system $mkdir unless $recon;
      my $command = qq[$op "$_->{n}" "$dest"];
      say $command if ($verbose || $recon);
      system $command unless $recon;
    }
  }
}

# Define filesystem functions.
{
  no strict 'refs';
  my @defs = (
    [ 'cp', 'cp' ],
    [ 'mv', 'mv' ],
    [ 'tmv', 'tmsu-fs-mv' ]
  );
  for my $d (@defs) {
    my $function_name = $d->[0];
    *$function_name = filesystem_fn($d->[1]);
  }
}

# print file metadata like harvest does
sub print_file {
  my $file = shift;
  printf("%-7d%-7s%-7s%-11s%-6s %s\n",
    $file->{i},
    $file->{nt},
    $file->{t},
    $file->{d},
    $file->{s},
    $file->{n}
  );
}

my $help = q{A wrapper around dyne/harvest to fill in missing functionality

Usage:

  hvst
  hvst help
  hvst ls [TYPE] [OPTION]
  hvst types
  hvst cp <TYPE> <DEST> [OPTION]...
  hvst mv <TYPE> <DEST> [OPTION]...
  hvst tmv <TYPE> <DEST> [OPTION]...

Examples:

  # Display help message.
  hvst help

  # List files in current directory.
  hvst

  # Also list files in current directory.
  hvst ls

  # List only audio files.
  hvst ls audio

  # List only book files WITHOUT metadata.  (filename only)
  hvst ls book -1

  # List types of files that exist in this directory and their counts.
  hvst types

  # Copy all images to ~/Pictures.
  hvst cp image ~/Pictures

  # Copy all images to ~/Pictures and be verbose.
  hvst cp image ~/Pictures --verbose

  # Show what would happen if we tried to move all images to ~/Pictures/YYYY/MM.
  hvst mv image @'"~/Pictures/$year/$month"' --recon

  # Move all videos to ~/Videos/YYYY/MM.
  hvst mv video @'"~/Videos/$year/$month"'

  # Move all books to ~/Dropbox/books using tmsu-fs-mv.
  hvst tmv book ~/Dropbox/books

  # The cp, mv, and tmv commands take --verbose and --recon
  # (or -v and -r for short).
  # --verbose means print the shell command.
  # --recon   means print the shell command but don't execute it.
};

sub main {
  if (@ARGV) {
    my $command = shift @ARGV;
    $_{verbose} = '';
    $_{recon} = '';
    if ($command eq 'cp') {
      GetOptions(\%_, "verbose|v", "recon|r");
      my ($type, $dest) = @ARGV;
      cp($type, $dest, $_{verbose}, $_{recon});
    } elsif ($command eq 'mv') {
      GetOptions(\%_, "verbose|v", "recon|r");
      my ($type, $dest) = @ARGV;
      mv($type, $dest, $_{verbose}, $_{recon});
    } elsif ($command eq 'tmv') {
      GetOptions(\%_, "verbose|v", "recon|r");
      my ($type, $dest) = @ARGV;
      tmv($type, $dest, $_{verbose}, $_{recon});
    } elsif ($command eq 'ls') {
      GetOptions(\%_, "1");
      my ($type) = @ARGV;
      my @files;
      if ($type) {
        @files = ls($type);
      } else {
        @files = harvest();
      }
      for (@files) {
        if ($_{1}) {
          say $_->{n};
        } else {
          print_file($_);
        }
      }
    } elsif ($command eq 'types') {
      my @types = sort { $b->[1] <=> $a->[1] } @{types()};
      for (@types) {
        printf(qq{%-8s%8d\n}, $_->[0], $_->[1]);
      }
    } elsif ($command eq "help" || $command eq "-h" || $command eq "--help") {
      print $help;
    } else {
      warn("Command '$command' not recognized.");
      return 1;
    }
  } else {
    my @files = harvest();
    for (@files) {
      print_file($_);
    }
  }
  return 0;
}

# https://stackoverflow.com/questions/707022/is-there-a-perl-equivalent-to-pythons-if-name-main
unless (caller) {
  exit main();
}

1;
